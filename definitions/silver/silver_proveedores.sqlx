config {
  type: "table",
  database: "fsp-pipeline-project",
  schema: "fsp_silver",
  name: "silver_proveedores",
  tags: ["silver"],
  description: "Tabla Silver estandarizada para proveedores.",
  columns: {
    id_proveedor: "Identificador único del proveedor.",
    nombre_proveedor: "Nombre del proveedor.",
    tipo_proveedor: "Tipo o categoría del proveedor (normalizada).",
    nit: "Nit del proveedor.",
    nombre_contacto: "Contacto principal del proveedor.",
    correo: "Correo normalizado.",
    telefono: "Teléfono normalizado.",
    ciudad: "Ciudad del proveedor.",
    fecha_ingesta: "Fecha de ingestión.",
    fuente: "Origen del dato."
  }
}

WITH base AS (

  SELECT
    SAFE_CAST(id_proveedor AS INT64) AS id_proveedor,

    TRIM(nombre_proveedor) AS nombre_proveedor,

    -- ============================
    -- RECATEGORIZACIÓN PROVEEDOR
    -- ============================
    CASE 
      WHEN LOWER(TRIM(tipo_proveedor)) IN ("empaques", "veterinaria") 
        THEN "servicios médicos"
      WHEN LOWER(TRIM(tipo_proveedor)) = "exámenes" 
        THEN "laboratorio"
      WHEN LOWER(TRIM(tipo_proveedor)) = "insumos" 
        THEN "transporte"
      WHEN LOWER(TRIM(tipo_proveedor)) = "tecnologia" 
        THEN "marketing"
      ELSE LOWER(TRIM(tipo_proveedor))
    END AS tipo_proveedor,

    TRIM(nit) AS nit,

    TRIM(nombre_contacto) AS nombre_contacto,

    CASE 
      WHEN correo IS NULL THEN 'desconocido'
      ELSE LOWER(TRIM(correo))
    END AS correo,

    CASE 
      WHEN telefono IS NULL THEN 'desconocido'
      ELSE TRIM(telefono)
    END AS telefono,

    CASE 
      WHEN ciudad IS NULL THEN 'bogota'
      ELSE LOWER(TRIM(ciudad))
    END AS ciudad,

    CURRENT_TIMESTAMP() AS fecha_ingesta,
    'raw_proveedores' AS fuente

  FROM ${ref("raw_proveedores")}

  WHERE 
    id_proveedor IS NOT NULL
    AND nombre_proveedor IS NOT NULL
    AND tipo_proveedor IS NOT NULL
    AND nit IS NOT NULL
    AND nombre_contacto IS NOT NULL
)

SELECT * FROM base